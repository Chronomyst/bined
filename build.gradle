buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    if (!gradle.startParameter.taskNames.contains("offline")) {
        dependencies {
            classpath "at.bxm.gradleplugins:gradle-svntools-plugin:latest.integration"
        }
    }
}

plugins {
    id "nebula.ospackage" version "4.5.1"
}

version = '0.1.3'

if (!gradle.startParameter.taskNames.contains("offline")) {
    apply plugin: 'at.bxm.svntools'
    apply plugin: "nebula.ospackage"
}

['base', 'distribution', 'maven'].each {
    apply plugin: it
}

task wrapper(type: Wrapper, description: 'Creates and deploys the Gradle wrapper to the current directory.') {
    gradleVersion = '3.2'
}

task('offline') {
}

task('cleanDeps').doLast {
    println "Deleting temporary dependency modules"
    File depsCheckFile = rootProject.file("deps/dependency.txt")
    if (depsCheckFile.exists()) {
        File depsDir = rootProject.file("deps")
        depsDir.listFiles().each() { childFile ->
            if (childFile.name != '_placeholder' && childFile.isDirectory()) {
                project.delete(childFile)
            }
        }
    } else {
        throw new Exception('Invalid dependency path')
    }
}

if (!gradle.startParameter.taskNames.contains("offline") && gradle.startParameter.taskNames.contains("clean") && !project.gradle.startParameter.projectProperties.containsKey("moduleDep")) {
    cleanDeps.execute()
}

subprojects.each { subproject -> evaluationDependsOn(subproject.path) }

subprojects {
    task allDeps(type: DependencyReportTask) {}
}

def createSvnDownloadTask(def moduleName, def svnUrlValue, def targetDirValue) {
    return tasks.create("svnDownload${moduleName}", at.bxm.gradleplugins.svntools.tasks.SvnExport) {
        svnUrl = svnUrlValue
        targetDir = targetDirValue
    }
}

if (file('deps/_placeholder').exists()) {
    // Perform restart for new dependency handling
    def depsLevel
    if (project.gradle.startParameter.projectProperties.containsKey('moduleDep')) {
        depsLevel = project.gradle.startParameter.projectProperties['moduleDep'] + 1
    } else {
        depsLevel = 1
    }
    def artefactBuild = project.tasks.create([name: "depsRebuild_${depsLevel}", type: GradleBuild])
    artefactBuild.buildFile = project.file('build.gradle')
    artefactBuild.tasks = project.gradle.startParameter.taskNames

    def artefactProjectProperties = artefactBuild.startParameter.projectProperties
    def currentProjectProperties = project.gradle.startParameter.projectProperties
    artefactProjectProperties << currentProjectProperties
    artefactProjectProperties << ['moduleDep': depsLevel]

    // Terminate currently invalid tasks
    // TODO: Fix support for other tasks like distZip
    subprojects.each { subproject ->
        if (subproject.tasks.findByPath('assemble') != null) {
            subproject.tasks['assemble'].setDependsOn([])
        }
    }
    if (project.tasks.findByPath('assemble') != null) {
        project.tasks['assemble'].setDependsOn([])
    }

    artefactBuild.execute()
}

if (!gradle.startParameter.taskNames.contains("offline")) {
    distZip.dependsOn project(':tools:deltahex-editor').tasks['launch4j']
}

ospackage {
    packageName = 'deltahex-editor'
    version = '0.1.3'
    release = '0'
    arch = NOARCH
    os = LINUX
    packageGroup = 'Converted/utils'
    license = 'Apache V2'
    vendor = 'ExBin Project'
    description = 'Free and open source hexadecimal viewer/editor and component written in Java.'
    distribution = 'Linux'
    url = 'http://deltahex.exbin.org'

    from('src/rpm/deltahex-editor') {
        into '/usr/bin'
    }

    from('src/rpm/deltahex-editor.desktop') {
        into '/usr/share/applications'
    }
    
    from('src/rpm/deltahex-editor.png') {
        into '/usr/share/pixmaps'
    }
    
    from(project(':tools:deltahex-editor').jar) {
        rename { filename -> project(':tools:deltahex-editor').name + ".jar" }
        into '/usr/share/deltahex-editor'
    }

    project(':tools:deltahex-editor').plugins.withType(JavaPlugin) {
        from(project(':tools:deltahex-editor').configurations.runtime) {
            into('/usr/share/deltahex-editor/lib')
            include '*'
        }
    }
}

buildRpm {
    requires('jre', '1.7.0', GREATER | EQUAL)
}

buildDeb {
    arch = 'all'
    requires('default-jre', '1.7.0', GREATER | EQUAL)
}

if (!project.gradle.startParameter.projectProperties.containsKey("moduleDep")) {
    distZip {
        archiveName project(':tools:deltahex-editor').name + '-' + version + '.zip'
    }

    distributions {
        main {
            contents {
                duplicatesStrategy = 'exclude'
                from project(':tools:deltahex-editor').jar

                from project.rootDir
                include 'LICENSE-2.0.txt'
                include 'changes.txt'

                from 'src/dist'
                include 'readme.txt'
                include 'deltahex-editor.sh'
//                include 'deltahex-editor.bat'
                include 'plugins/*'
                
                if (!gradle.startParameter.taskNames.contains("offline")) {
                    from project(':tools:deltahex-editor').projectDir.absolutePath + '/build/launch4j'
                    include "DeltaHexEditor.exe"
                }

                //            into('lib') {
                //                from 'lib'
                //            }

                into('') {
                    from project(':tools:deltahex-editor').jar.archivePath
                    include '*'
                    rename { filename -> project(':tools:deltahex-editor').name + ".jar" }
                }

                project(':tools:deltahex-editor').plugins.withType(JavaPlugin) {
                    into('lib') {
                        from project(':tools:deltahex-editor').configurations.runtime
                        include '*'
                    }
                }

                //            into('doc') {
                //                from 'doc'
                //                include '**'
                //            }
                //            into('resources') {
                //                from 'resources'
                //                include '**'
                //                exclude 'private'
                //            }
            }
        }
    }
}

